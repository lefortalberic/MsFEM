//mpirun -np 1 FreeFem++-mpi -wg main_erreur_phi_psi_cellule_unite_filtre.edp -v 0


string nameMethod = "Psi_Phi_carre_filtre";

include "init.edp"
///////////////////////////////////////////////////////////////////////
// Define internal data structures                                   //
///////////////////////////////////////////////////////////////////////

////////// Diffusion coefficient //////////

/* func Aeps = 6+5*cos(2*(pi/eps)*(x+2*y))*sin(2*(pi/eps)*(x-y));

func Ay =6+5*cos(2*pi*(x+2*y))*sin(2*pi*(x-y));

func Aeps = 6+5*sin(2*(pi/eps)*x);

func Ay =6+5*sin(2*pi*x);

func sigmaeps = 20*(sin((pi*x)/eps)*sin((pi*x)/eps)+2);

func sigmay = 20*(sin((pi*x))*sin((pi*x))+2);

//func FiltrePhi= 1;
func FiltrePhi= (1-x)*(1-x)*x*x*(1-y)*(1-y)*y*y*900;
//////////////////////////////////////////////////////////////// */
include "Psi_triche.edp"

Psieps = VecteurproprePsi(fmod(x/eps,1), fmod(y/eps,1));

if (plots){ plot(Psieps, fill=1, value=1, cmm="Psi", wait=1);}
// with Filtrage
varf oploc (u, v)
        = int2d(Th)(
            FiltrePhi*sigmaeps(x,y)*u*v)
        + int2d(Th)(eps^2*FiltrePhi*a(Aeps(x,y),u,v))
        ;

varf bloc (u, v) = int2d(Th)(FiltrePhi*u*v); //no boundary condition


varf CLXfiltreloc (unused, v)
    = int2d(Th)(FiltrePhi*1*dx(v)) 
    ;

varf CLYfiltreloc (unused, v)
    = int2d(Th)(FiltrePhi*1*dy(v)) 
    ;

real[int] CLXfiltreloclocS1 = CLXfiltreloc(0, Vh);
real[int] CLYfiltreloclocS1 = CLYfiltreloc(0, Vh);

real[int] Zeros = 0*CLXfiltreloclocS1;
//cout << "CRlocS1z = " << CRlocS1z.n << endl;

matrix AlocS = oploc(Vh, Vh, factorize=1); 
matrix BlocS = bloc(Vh, Vh, solver=CG, eps=1e-20);

matrix AlocStmp(AlocS.n+2,AlocS.m+2);
matrix BlocStmp(BlocS.n+2,BlocS.m+2);
AlocStmp = [ [AlocS , CLXfiltreloclocS1 , CLYfiltreloclocS1] , [CLXfiltreloclocS1' , 0, 0] , [CLYfiltreloclocS1' , 0 ,0] ];
BlocStmp = [ [BlocS , Zeros, Zeros ] , [Zeros' , 0, 0 ] , [Zeros' , 0 , 0] ];

real[int] Listelambdaloc(1); //to store the nev eigenvalue (nev = 1 ici)
real[int,int] ListeVecteurproprelocS(AlocStmp.n,1) ; //to store the nev eigenvector

Mat Aloctemp = AlocStmp;
Mat Bloctemp = BlocStmp;

//int kloc = EPSSolve(Bloctemp, Aloctemp, values=Listelambdaloc , array=ListeVecteurproprelocS, sparams= "-eps_type lapack"); //Résolution du pb aux vp
int kloc = EPSSolve(Aloctemp, Bloctemp, values=Listelambdaloc , array=ListeVecteurproprelocS, sparams= ssparamsfiltre); //Résolution du pb aux vp
kloc=min(kloc,1);

cout << "VP" << kloc << " = " << Listelambdaloc(0) << endl;
Vh VecteurproprelocS;

VecteurproprelocS[](0:VecteurproprelocS.n-1) = ListeVecteurproprelocS(0:VecteurproprelocS.n-1,0);
real LagrangeMultiplier1 = ListeVecteurproprelocS(VecteurproprelocS.n,0);
real LagrangeMultiplier2 = ListeVecteurproprelocS(VecteurproprelocS.n+1 ,0);

cout << "LagrangeMultiplier1 = " << LagrangeMultiplier1 << endl;
cout << "LagrangeMultiplier2 = " << LagrangeMultiplier2 << endl;

if (VecteurproprelocS(Lx/2 , Ly/2)<0)  //Evaluation au point milieu
{
    VecteurproprelocS = -VecteurproprelocS;
}

// Renormalisation
real normeL2VecteurproprelocS=sqrt(int2d(Th)(VecteurproprelocS*VecteurproprelocS));
VecteurproprelocS[] = VecteurproprelocS[]/normeL2VecteurproprelocS;
///////////////////////////////////////////////////////////////////////

if (plots){ plot(VecteurproprelocS, fill=1, value=1, cmm="Premier vecteur propre", wait=1);}

///////////////////////////////////////////////////////////////////////
// Compute the error                                                //
Vh e=0;
Vh erreurH1 = 0;
e[] = VecteurproprelocS[] - Psieps[];
erreurH1 = dx(e)*dx(e) + dy(e)*dy(e) + e*e;

real eH1int = int2d(Th)(dx(e)*dx(e)+dy(e)*dy(e) + e*e)/ int2d(Th)(dx(Psieps)*dx(Psieps)+dy(Psieps)*dy(Psieps) + Psieps*Psieps);
cout << "eH1_Omega = " << sqrt(eH1int) << endl;


if (plots){ plot(e, fill=1, value=1, cmm="Erreur", wait=1);}
if (plots){ plot(erreurH1, fill=1, value=1, cmm="Erreur H1", wait=1);}

real eH1, eMax , nH1, nMax;
real eH1filtre, nH1filtre;

real eVP;

real eH1Interieur, eMaxInterieur , nH1Interieur, nMaxInterieur;
real eH1Interieurfiltre, nH1Interieurfiltre;

eH1=int2d(Th)(dx(e)*dx(e)+dy(e)*dy(e) + e*e);
nH1 = int2d(Th)(dx(Psieps)*dx(Psieps)+dy(Psieps)*dy(Psieps) + Psieps*Psieps);

eH1filtre = int2d(Th)(FiltrePhi*(dx(e)*dx(e)+dy(e)*dy(e) + e*e));
nH1filtre = int2d(Th)(FiltrePhi*(dx(Psieps)*dx(Psieps)+dy(Psieps)*dy(Psieps) + Psieps*Psieps));

eMax=e[].max;
nMax=Psieps[].max;

// Taking square roots, determining the relative errors
eH1 = sqrt(eH1/nH1);
eH1filtre = sqrt(eH1filtre/nH1filtre);
eMax=eMax/nMax;

for (int k=0; k<Ntri; k++) {

    vk[][k]=1;
    
    // Defining the vertices of the ideal oversampling patch (possibly lying partially outside the global domain)
    real[int,int] Kx(3,2); //save coordinates of the vertices belonging to K
    for(int kk=0; kk<3; kk++) {
        Kx(kk,0) = TH[k][kk].x;
        Kx(kk,1) = TH[k][kk].y;
    }
    real xCL = (Kx(:,0).min); //we take a small margin around the vertices of S as computed above

    real xCR = (Kx(:,0).max);

    real yCL = (Kx(:,1).min);

    real yCR = (Kx(:,1).max);


    int[int] n2oFine;
    mesh Kh=trunc(Th, vk>0.5, split=1, new2old=n2oFine);
    fespace VK(Kh,P1);
    int[int] loc2globFine = restrict(VK, Vh, n2oFine);

    if ((xCL!=0) && (xCR !=1) && (yCL!=0) && (yCR !=1) ){
        //plot(Kh, vk, fill=1, cmm="Mesh element and oversampling patch", wait=1);
        VK VecteurproprelocK=0;
        VecteurproprelocK[] = VecteurproprelocS[](loc2globFine);

        // Renormalisation
        real normeL2locK=sqrt(int2d(Kh)(VecteurproprelocK*VecteurproprelocK));
        VecteurproprelocK[] = VecteurproprelocK[]/(normeL2locK/sqrt(int2d(Kh)(1)));

        VK eK=0;
        VK PsiepsK=0;
        PsiepsK[] = Psieps[](loc2globFine);

        //renormalisation
        real normeL2PsiepsK=sqrt(int2d(Kh)(PsiepsK*PsiepsK));
        PsiepsK[] = PsiepsK[]/(normeL2PsiepsK/sqrt(int2d(Kh)(1)));

        eK[] = PsiepsK[];
        eK[] = eK[] - VecteurproprelocK[];

        VK erreurH1K = 0;
        erreurH1K = dx(eK)*dx(eK) + dy(eK)*dy(eK) + eK*eK;
        /*plot(VecteurproprelocK, fill=1, value=1, cmm="Premier vecteur propre", wait=1);
        plot(PsiepsK, fill=1, value=1, cmm="Psi", wait=1);
        plot(eK, fill=1, value=1, cmm="Erreur", wait=1);
        plot(erreurH1K, fill=1, value=1, cmm="Erreur H1", wait=1);*/

        eH1Interieur += int2d(Kh)(dx(eK)*dx(eK)+dy(eK)*dy(eK) + eK*eK);
        nH1Interieur += int2d(Kh)(dx(PsiepsK)*dx(PsiepsK)+dy(PsiepsK)*dy(PsiepsK) + PsiepsK*PsiepsK);

        //cout << "eH1Interieur = " << int2d(Kh)(dx(eK)*dx(eK)+dy(eK)*dy(eK) + eK*eK) << endl;
        //cout << "nH1Interieur = " << int2d(Kh)(dx(PsiepsK)*dx(PsiepsK)+dy(PsiepsK)*dy(PsiepsK) + PsiepsK*PsiepsK) << endl;

        eH1Interieurfiltre += int2d(Kh)(FiltrePhi*(dx(eK)*dx(eK)+dy(eK)*dy(eK) + eK*eK));
        nH1Interieurfiltre += int2d(Kh)(FiltrePhi*(dx(PsiepsK)*dx(PsiepsK)+dy(PsiepsK)*dy(PsiepsK) + PsiepsK*PsiepsK));

        eMaxInterieur =max(eK[].max,eMaxInterieur);
        nMaxInterieur =max(PsiepsK[].max,nMaxInterieur);
}
vk[][k]=0;       
}
eH1Interieur = sqrt(eH1Interieur/nH1Interieur);
cout << "eH1Interieur = " << eH1Interieur << endl;
eH1Interieurfiltre = sqrt(eH1Interieurfiltre/nH1Interieurfiltre);
eMaxInterieur = eMaxInterieur/nMaxInterieur;

eVP = abs(Listelambdaloc(0)- lambdaPsi)/(lambdaPsi);
cout << "eVP = " << eVP << endl;

// Writing errors on K to an external file
string errname = "Results/erreur_Cellule_unite_psi_phi_filtre" + "_Coeffs_" + NumCoeffDiff + ".txt";
ofstream ff(errname, append);
ff  << ParameterDescription << "n\t" << n << "\t"
    << "N\t" << Ndiscr << "\t"
    << "NumVP\t" << NumberEigenValue << "\t"
    << "osCoef\t" << osCoef << "\t"
    << endl;

ff  << "\t"
    << "eps\t" << eps << "\t"
    << "eH1\t" << eH1 << "\t"
    << "eH1filtre\t" << eH1filtre << "\t"
    << "eMax\t" << eMax << "\t"
    << "eH1Interieur\t" << eH1Interieur << "\t"
    << "eH1Interieurfiltre\t" << eH1Interieurfiltre << "\t"
    << "eMaxInterieur\t" << eMaxInterieur << "\t"
    << "VP1\t" << eVP << "\t"
    << endl;

ff<<endl;

printmessage("////////////////////////////////////////////////////////////////");